plugins {
    id 'java'
    id 'eclipse'
	id 'org.springframework.boot' version '2.7.4'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

version = "${version}"
description = 'SolarSSH Server'
archivesBaseName = 'solarssh'

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
}

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    jcenter()
    mavenLocal()
    maven { url "https://dev.solarnetwork.net/archive/repository/solarnetwork-stage/" }
}

ext {
	bouncyCastleVersion 	= '1.57'
	checkstyleVersion 		= '8.34'
    myBatisStarterVersion 	= '2.1.3'
    sshdVersion				= '2.5.1'

    libraries = [
        // Test dependencies.
        hamcrest: 'org.hamcrest:hamcrest-library:1.3',
        junit: 'junit:junit:4.12',
        mockito: 'org.mockito:mockito-core:2.25.1'
    ]
}

configurations.compile {
	exclude group: 'commons-logging'
	exclude group: 'net.sf.supercsv'
	exclude group: 'org.apache.tomcat', module: 'tomcat-jdbc'
	exclude group: 'org.osgi'
	exclude group: 'org.springframework', module: 'spring-messaging'
	exclude group: 'org.springframework.security'
	exclude group: 'javax.servlet', module: 'com.springsource.javax.servlet'
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	// JSON
	implementation "com.fasterxml.jackson.core:jackson-annotations"
	implementation "com.fasterxml.jackson.core:jackson-core"
	implementation "com.fasterxml.jackson.core:jackson-databind"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda"
	
	// DB
	implementation "org.postgresql:postgresql"

	// Logging
	implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"
	implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
	implementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
	implementation "org.slf4j:slf4j-api:${slf4jVersion}"
	
	// SSH
    implementation "org.apache.sshd:sshd-mina:${sshdVersion}"
    implementation "org.bouncycastle:bcpg-jdk15on:${bouncyCastleVersion}"
    implementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"
    implementation 'net.i2p.crypto:eddsa:0.3.0'

	// SolarNetwork
	implementation 'net.solarnetwork.common:net.solarnetwork.common:2.10+'
	implementation 'net.solarnetwork.common:net.solarnetwork.common.web:2.2+'
	implementation 'net.solarnetwork.central:net.solarnetwork.central.common.security:1.16+'
	
    // HTTP
    compile 'org.mitre.dsmiley.httpproxy:smiley-http-proxy-servlet:1.9'
    
    // Testing
	// Testing
    testImplementation 	libraries.junit,
		        		libraries.hamcrest,
		                libraries.mockito
	testImplementation 'ch.qos.logback:logback-classic:1.2.3'
}

jar {
    baseName = 'solarssh'
    version =  version
    manifest {
        attributes 'Implementation-Version': version
    }
}

[
    compileJava,
    compileTestJava
].each() {
    it.options.compilerArgs += [
        "-Xlint:all",
        "-Xlint:-options",
        "-Xlint:-path",
        "-Xlint:-try"
    ]
    it.options.encoding = "UTF-8"
    if (rootProject.hasProperty('failOnWarnings') && rootProject.failOnWarnings.toBoolean()) {
        it.options.compilerArgs += ["-Werror"]
    }
}

compileTestJava {
    options.compilerArgs += [
        "-Xlint:-serial"
    ]
}

jar.manifest {
    attributes('Implementation-Title': name,
    'Implementation-Version': version,
    'Built-By': System.getProperty('user.name'),
    'Built-JDK': System.getProperty('java.version'),
    'Source-Compatibility': sourceCompatibility,
    'Target-Compatibility': targetCompatibility)
}

javadoc.options {
    encoding = 'UTF-8'
    use = true
    links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    source = "17"
}

eclipse {
	project {
		natures 'net.sf.eclipsecs.core.CheckstyleNature'
		buildCommand 'net.sf.eclipsecs.core.CheckstyleBuilder'
	}
	jdt {
		javaRuntimeName 'JavaSE-17'
	}
}

// Disable JavaDoc doclint on Java 8. It's annoying.
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// For jdk10 we must explicitly choose between html4 and html5, otherwise we get a warning
if (JavaVersion.current().isJava10Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addBooleanOption('html5', true)
        }
    }
}

checkstyle {
    toolVersion = "${checkstyleVersion}"
	configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    ignoreFailures = false
    if (rootProject.hasProperty("checkstyle.ignoreFailures")) {
        ignoreFailures = rootProject.properties["checkstyle.ignoreFailures"].toBoolean()
    }
}

checkstyleMain {
    source = fileTree(dir: "src/main", include: "**/*.java")
}

checkstyleTest {
    source = fileTree(dir: "src/test", include: "**/*.java")
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    archiveClassifier = 'sources'
}

// At a test failure, log the stack trace to the console so that we don't
// have to open the HTML in a browser.
test {
    testLogging {
        exceptionFormat = 'full'
        showExceptions true
        showCauses true
        showStackTraces true
    }
    maxHeapSize = '1500m'
}
